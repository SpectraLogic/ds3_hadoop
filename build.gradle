/*
 * ******************************************************************************
 *   Copyright 2014-2015 Spectra Logic Corporation. All Rights Reserved.
 *   Licensed under the Apache License, Version 2.0 (the "License"). You may not use
 *   this file except in compliance with the License. A copy of the License is located at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 *   or in the "license" file accompanying this file.
 *   This file is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 *   CONDITIONS OF ANY KIND, either express or implied. See the License for the
 *   specific language governing permissions and limitations under the License.
 * ****************************************************************************
 */


allprojects {
    group = 'com.spectralogic.ds3-hadoop'
    version = '0.8.2'
}

subprojects {
    buildscript {
      repositories { jcenter() }
      dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
      }
    }

    apply plugin: "java"
    apply plugin: "maven"

    sourceCompatibility = JavaVersion.VERSION_1_7


    compileJava {
        options.compilerArgs << '-Xlint:deprecation'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'http://dl.bintray.com/spectralogic/ds3'
        }
    }


    dependencies {
        compile "org.apache.hadoop:hadoop-hdfs:2.6.0"
        compile "org.apache.hadoop:hadoop-common:2.6.0"
        compile "org.apache.hadoop:hadoop-mapreduce-client-core:2.6.0"

        compile ('com.spectralogic.ds3:ds3-sdk:1.1.0-RC6:all') {
            transitive = false
        }
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:1.+'
    }
}

project(':ds3-hadoop-cli') {
    configurations {
        libdeps
    }

    dependencies {
        compile fileTree(dir: 'lib', include: '*.jar')
        compile project(path: ':ds3-hadoop-core', configuration: 'fat')
        libdeps project(path: ':ds3-hadoop-core', configuration: 'fat')
        libdeps fileTree(dir: 'lib', include: '*.jar')
    }

    task copyToLib(type: Copy) {
        from configurations.libdeps
        into "$buildDir/resources/main/lib"
    }

    jar.dependsOn copyToLib

    jar {
        manifest {
            attributes("Main-Class": "com.spectralogic.ds3.hadoop.cli.FileMigrator")
        }
    }

}

project(':ds3-hadoop-samples') {
    dependencies {
        compile project(path: ':ds3-hadoop-core', configuration: 'fat')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}
