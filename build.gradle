/*
 * ******************************************************************************
 *   Copyright 2014 Spectra Logic Corporation. All Rights Reserved.
 *   Licensed under the Apache License, Version 2.0 (the "License"). You may not use
 *   this file except in compliance with the License. A copy of the License is located at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 *   or in the "license" file accompanying this file.
 *   This file is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 *   CONDITIONS OF ANY KIND, either express or implied. See the License for the
 *   specific language governing permissions and limitations under the License.
 * ****************************************************************************
 */


allprojects {
    group = 'com.spectralogic.ds3-hadoop'
    version = '0.7.0-STUB'
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven"

    sourceCompatibility = JavaVersion.VERSION_1_7

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'http://spectralogic.github.io/java/repository'
        }
    }

    def hadoopVersion = '2.5.0'

    dependencies {
        compile 'javax.activation:activation:1.1'
        compile "org.apache.hadoop:hadoop-common:$hadoopVersion"
        compile "org.apache.hadoop:hadoop-mapreduce-client-core:$hadoopVersion"
        compile "org.apache.hadoop:hadoop-hdfs:$hadoopVersion"
        runtime "org.apache.hadoop:hadoop-mapreduce-client-jobclient:$hadoopVersion"
        compile 'com.spectralogic.ds3:ds3-sdk:0.7.6-SNAPSHOT'
        compile 'com.google.guava:guava:16.0.1'
        testCompile 'junit:junit:4.11'
    }
}

project(':ds3-hadoop-cli') {
    task copyToLib(type: Copy) {
        from configurations.runtime
        into "$buildDir/resources/main/lib"
    }

    jar.dependsOn copyToLib

    jar {
        manifest {
            attributes("Main-Class": "com.spectralogic.ds3.hadoop.com.spectralogic.ds3.hadoop.cli.FileMigrator")
        }
    }

    dependencies {
        compile fileTree(dir: 'lib', include: '*.jar')
        compile project(':ds3-hadoop-core')
    }
}

project(':ds3-hadoop-samples') {
    dependencies {
        compile project(':ds3-hadoop-core')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}
